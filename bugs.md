# Bugs

O exame da ROM do MC1000 revela algumas caracter√≠sticas que foram programadas, mas que n√£o funcionam devido a erros de l√≥gica.

Para cada erro apresenta-se um ‚Äúpatch‚Äù, um ‚Äúremendo‚Äù, que poderia consertar o erro numa vers√£o melhorada da ROM.

## Caracter de controle FF (`CHR$(12)`)

Pela programa√ß√£o na ROM, o caracter de controle FF deveria avan√ßar o cursor, isto √©, mov√™-lo uma coluna √† direita; se estivesse no fim de uma linha, iria para o in√≠cio da linha seguinte; e se estivesse no fim da tela, continuaria no mesmo lugar e um bip soaria para avisar da impossibilidade do cursor avan√ßar.

Mas h√° um erro na rotina de verifica√ß√£o de fim de tela. A posi√ß√£o do cursor √© subtra√≠da em um (equivalendo a um movimento para a esquerda), e quando finalmente se soma um para faz√™-lo mover-se √† direita... o resultado √© que o cursor n√£o sai do lugar!

```
pc ‚Üê POSI√á√ÉO_DO_CURSOR
ft ‚Üê FIM_DA_TELA + 1
pc ‚Üê pc - 1
(deveria ser: ft ‚Üê ft - 1)
se pc = ft ent√£o
   produz bip
sen√£o
   pc ‚Üê pc + 1
   POSI√á√ÉO_DO_CURSOR ‚Üê pc
fim se
```

Como efeito colateral, este bug inutilizou o uso das teclas \<H>, \<J>, \<K> e \<L> associadas a \<CTRL> para mover o cursor enquanto se digita uma linha. A l√≥gica est√° toda implementada, e quando se pressiona \<CTRL>+\<L> a posi√ß√£o do cursor √© at√© avan√ßada no buffer de linha, mas n√£o se move na tela.

### Patch

```
CA69  2B        DEC HL
```
- - -
```
CA69  1B        DEC DE
```

## Caracter de controle DEL (`CHR$(127)`)

DEL deveria mover o cursor para a esquerda, apagando (isto √©, substituindo por espa√ßo) o caracter que estivesse na posi√ß√£o do cursor. Mas uma verifica√ß√£o mal feita faz com que a por√ß√£o de c√≥digo que interpreta DEL como um caracter de controle nunca seja executada. Por isso ele acaba sendo interpretado como um caracter comum.

```
c ‚Üê CARACTER_A_IMPRIMIR
se c < 32 ent√£o
(deveria ser: se c < 32 ou c = 127 ent√£o)
   (interpreta caracteres de controle)
   se c = 7 ent√£o
      produz bip
   fim se
   (...)
   se c = 127 ent√£o
      (...)
   fim se
sen√£o
   imprime c como um caracter comum
fim se
```

### Patch


```
C8BE  C2F9C8    JP      NZ,$C8F9
```
- - -
```
C8BE  C2F4C8    JP      NZ,$C8F4
```

## Posicionamento de cursor (`CHR$(27)`)

Usando `CHR$(27)` (ESC) √© poss√≠vel posicionar o cursor no MC1000. Para programar o cursor em modo de 32 colunas, dever-se-ia usar `CHR$(27);"=";CHR$(`*linha*`);CHR$(`*coluna*`)`. E em modo de 80 colunas, `CHR$(27);"=";CHR$(`*linha*`);CHR$(32+`*coluna*`)`.

Um erro faz com que o posicionamento de cursor para 32 colunas n√£o funcione corretamente: o cursor √© posicionado uma linha abaixo de onde deveria ficar.


```
li ‚Üê N√öMERO_DA_LINHA
co ‚Üê N√öMERO_DA_COLUNA
se co < 32 ent√£o
   (para 32 colunas)
   co ‚Üê co + 32
   (esta soma n√£o deveria ser feita!)
sen√£o
   (para 80 colunas)
   co ‚Üê co - 32
fim se
posiciona o cursor nas coordenadas (li,co)
```

### Como contornar

Use o posicionamento para 80 colunas (`CHR$(27);"=";CHR$(`*linha*`);CHR$(32+`*coluna*`)`). Ele funciona mesmo quando o v√≠deo est√° em 32 colunas.

### Patch

```
C994  C620      ADD     A,$20
```
- - -
```
C994  00        NOP
C995  00        NOP
```

## Instru√ß√£o `SLOW`

O MC1000 tem um par de instru√ß√µes n√£o documentadas no manual de BASIC: `FAST` e `SLOW`. `FAST` (‚Äúr√°pido‚Äù) √© o modo normal de execu√ß√£o do MC1000. O `SLOW` (‚Äúlento‚Äù) faz com que o computador adicione uma brev√≠ssima pausa entre os comandos. Mas, por um erro de l√≥gica, o comando n√£o funciona. Primeiro ele verifica (corretamente) se n√£o h√° nenhum par√¢metro a seguir (se houver, ele produz erro de sintaxe, ‚Äú?SN ERRO‚Äù); depois disso, ele ‚Äúinvade‚Äù a programa√ß√£o do comando SET, produzindo um erro de acordo com o modo de v√≠deo. Em modo TEXT ele produz erro de tipo incompat√≠vel (‚Äú?TI ERRO‚Äù); em modo gr√°fico (GR ou HGR) ele produz erro de falta de operando (‚Äú?FO ERRO‚Äù).

```
     FAST: x ‚Üê 0
FAST_SLOW: endere√ßo de mem√≥ria(864) ‚Üê x
           retorna
     SLOW: x ‚Üê 1
           (faltou: desvia para FAST_SLOW)
      SET: (...)
```

### Como contornar

√â poss√≠vel entrar em modo `SLOW` por meio da instru√ß√£o `POKE 864,1`.

### Patch

Este patch exige alguns bytes do espa√ßo n√£o utilizado no fim da ROM.

```
D74D  22D2      DB      $D222

F563  FFFFFF    DB      $FF,$FF,$FF ; (Lixo no fim da ROM)
F566  FFFFFF    DB      $FF,$FF,$FF
```
- - -
```
D74D  63F5      DB      $F563

F563  C0        RET     NZ
F564  3E01      LD      A,$01
F566  C31ED2    JP      $D21E
```

## Cl√°usula `VTAB`

`VTAB` √© uma cl√°usula da instru√ß√£o `PRINT` n√£o documentada no Manual do BASIC.

Deveria servir para posicionar o cursor verticalmente, mas n√£o funciona por uma sucess√£o de erros na l√≥gica que calcula a nova posi√ß√£o do cursor.

Tenta isto e v√™ o cursor desaparecer... üôÅ

```
PRINT VTAB(12);
```

Para recuperar o cursor, digita (√†s cegas mesmo) o comando `HOME` seguido da tecla \<RETURN>, ou ent√£o pressiona \<CTRL>+\<‚Üë>.

### Como contornar

Pode-se usar a sequ√™ncia de escape para posicionamento do cursor, obtendo-se a coluna atual do cursor da vari√°vel de sistema *LCNT*: `PRINT CHR$(27);"=";CHR$(`*linha*`);CHR$(32+PEEK(349));`

### Patch

```
E141  210080    LD      HL,$8000
E144  ED52      SBC     HL,DE
E146  13        INC     DE
E147  01        DB      $01 ; "LD BC,..." (oculta as duas instru√ß√µes seguintes)
E148  7B        LD      A,E
E149  3D        DEC     A
E14A  2803      JR      Z,$E14F
E14C  09        ADD     HL,BC
E14D  18FA      JR      $E149
```
- - -
```
E141  2A6301    LD      HL,($0163) ; SNAM = in√≠cio da VRAM.
E144  ED5B1301  LD      DE,($0113) ; DLNG = largura da linha.
E148  04        INC     B ; par√¢metro de VTAB() + 1.
E149  1801      JR      $E14C
E14B  19        ADD     HL,DE
E14C  10FD      DJNZ    $E14B
E14E  00        NOP
```

## Rotina *JSTICK*

Esta √© uma rotina da ROM do MC1000 (no endere√ßo $CC2C) voltada a permitir que um ou dois jogadores joguem compartilhando o teclado.

| Jogador  |  ü°Ñ   |  ü°Ö   |  ü°á   |  ü°Ü   | A√ß√£o                          |
| :------: | :--: | :--: | :--: | :--: | :---------------------------- |
| Esquerda | \<A> | \<S> | \<D> | \<F> | \<Q> \<W> \<E> \<R> \<T>      |
| Direita  | \<K> | \<L> | \<;> | \<:> | \<I> \<O> \<P> \<‚Üë> \<RETURN> |

A rotina traduz um c√≥digo de tecla para um byte cujos bits indicam a qual dos controles ela corresponde, se algum.

A listagem desta rotina na ROM est√° no Manual de Refer√™ncia, e vemos claramente que o erro surgiu por erro de digita√ß√£o de um dos r√≥tulos de desvio internos da rotina. Foi digitado ‚ÄúK6‚Äù em vez de ‚ÄúJ6‚Äù, e por azar o r√≥tulo ‚ÄúK6‚Äù existia em outro lugar da listagem, ent√£o o erro n√£o foi detectado durante a compila√ß√£o. O resultado foi um desvio para fora da rotina (para o endere√ßo $C3FB em vez de $CC83), inutilizando-a. Eis a listagem original (em assembly 8080) na p√°gina 96:

```
LOC  OBJ      LINE         SOURCE  STATEMENT
[...]
CC59 FE41     2175         CPI     'A'
CC5B C260CC   2176         JNZ     J1
CC5E 0604     2177         MVI     B,4
              2178 J1:
CC60 FE46     2179         CPI     'F'
CC62 C267CC   2180         JNZ     J2
CC65 0608     2181         MVI     B,8
              2182 J2:
CC67 FE53     2183         CPI     'S'
CC69 C26ECC   2184         JNZ     J3
CC6C 0610     2185         MVI     B,10H
              2186 J3:
CC6E FE43     2187         CPI     'C'
CC70 C275CC   2188         JNZ     J4
CC73 0610     2189         MVI     B,10H
              2190 J4:
CC75 FE44     2191         CPI     'D'
CC77 C27CCC   2192         JNZ     J5
CC7A 0620     2193         MVI     B,20H
              2194 J5:
CC7C FE4B     2195         CPI     'K'
CC7E C2FBC3   2196         JNZ     K6   (r√≥tulo errado, seria J6!)
CC81 0605     2197         MVI     B,5
              2198 J6:
CC83 FE3A     2199         CPI     ':'
[...]
```

(No assembly do Z80, `CPI` √© `CP`, `JNZ` √© `JP NZ` e `MVI` √© `LD`.)

### Patch

```
CC7E  C2FBC3    JP      NZ,$C3FB
```
- - -
```
CC7E  C283CC    JP      NZ,$CC83
```

## Instru√ß√µes `PLOT`, `UNPLOT`, `DRAW`, `UNDRAW`

No modo `HGR`, quando as instru√ß√µes de desenho tentam desenhar/apagar uma linha em 45¬∞ com uma extremidade na coordenada horizontal 255, as instru√ß√µes n√£o conseguem terminar de desenhar a linha e o computador trava.

Por exemplo, executar `HGR : PLOT 255-7,0 TO 255,7` deveria produzir apenas um pequeno tra√ßo diagonal no canto superior direito da tela, mas a linha continua sendo desenhada tela abaixo:

![](img/mc1000-bug-plot.png)

O problema ocorre porque nas rotinas espec√≠ficas que tratam as linhas em 45¬∞, identifica-se a chegada ao fim da linha quando, ap√≥s somar 1 ao registrador contendo a coordenada horizontal do pixel rec√©m desenhado, seu valor √© superior ao da coordenada final. Mas como o em 8 bits 255 + 1 = 0, seu valor NUNCA √© maior do que 255!

```
x ‚Üê MENOR_COORDENADA_X
y ‚Üê COORDENADA_Y_CORRESPONDENTE
fa√ßa
   desenha ponto nas coordenadas (x,y)
   x ‚Üê x + 1 (em 8 bits, 256 √© automaticamente transformado em 0)
   y ‚Üê y + 1
   se LINHA_ASCENDENTE
      y ‚Üê y - 2
   fim se
at√© que x > MAIOR_COORDENADA_X
(esta condi√ß√£o nunca √© satisfeita quando a MAIOR_COORDENADA_X √© 255!)
```

### Patch

A solu√ß√£o √© comparar a coordenada atual com a coordenada final antes de somar 1.

```
D2B6  7C        LD      A,H
D2B7  24        INC     H
D2B8  45        LD      B,L
D2B9  2C        INC     L
D2BA  CDDAD3    CALL    $D3DA ; PLOTAB
D2BD  F1        POP     AF
D2BE  F5        PUSH    AF
D2BF  3802      JR      C,$D2C3
D2C1  2D        DEC     L
D2C2  2D        DEC     L
D2C3  7C        LD      A,H
D2C4  B9        CP      C
D2C5  28EF      JR      Z,$D2B6
D2C7  38ED      JR      C,$D2B6
```
- - -
```
D2B6  7C        LD      A,H
D2B7  45        LD      B,L
D2B8  CDDAD3    CALL    $D3DA ; PLOTAB
D2BB  7C        LD      A,H
D2BC  B9        CP      C
D2BD  280A      JR      Z,$D2C9
D2BF  24        INC     H
D2C0  2C        INC     L
D2C1  F1        POP     AF
D2C2  F5        PUSH    AF
D2C3  38F1      JR      C,$D2B6
D2C5  2D        DEC     L
D2C6  2D        DEC     L
D2C7  18ED      JR      $D2B6
```
